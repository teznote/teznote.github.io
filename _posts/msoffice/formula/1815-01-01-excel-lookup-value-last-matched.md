---
layout: post
title: "INDEX,MATCH: 조건에 부합하는 가장 마지막 항목 검색"
updated: 2023-04-22
categories: [excel_formula]
tags: [msoffice,excel,formula,index,match]
---

## 조건 부합하는 가장 마지막 항목 검색 함수식

![그림00](/img/msoffice/formula/formula-1815.png)

```excel
{= INDEX( 검색대상열, MATCH( 2, 1/((조건식)*(조건식)* ... *(조건식)), 1 ))}
```
{:.excel}

수식 앞뒤의 중괄호는 실제 입력하는 것이 아닌 **배열수식**으로 입력했을 때 나오는 표시를 표현한 것이다. 위 수식은 배열수식으로 입력해야 한다.

`(조건식)` 을 계속 * 기호로 연결해주는 것만으로도 복수의 조건식을 만족하는 가장 마지막 항목을 찾는 것이 가능하다.

## 함수식 이해

`(조건식)` 의 결과는 True 혹은 False 이다. 그리고 엑셀은 이 값에 * 와 같은 사칙연산을 적용하면, True 는 1 로, False 는 0 으로 치환하여 계산을 한다. 따라서, `((조건식)*(조건식)* ... *(조건식))` 의 결과는 1 또는 0 이 된다. 모든 조건식이 True 인 경우에만 1 이 될 것이다.

이를 `1/ 결과` 와 같은 형태로 나눗셈을 한다. 만일 결과가 1 이었다면 나눗셈 결과는 1 이고, 결과가 0 이었다면 #DIV/0! 에러가 나오게 된다.

이를 MATCH 함수가 받는데, 위 엑셀 그림의 경우라면 `MATCH(2, {#DIV/0!, #DIV/0!, #DIV/0!, #DIV/0!, #DIV/0!, 1, #DIV/0!, #DIV/0!, 1, #DIV/0!}, 1)` 과 같은 형태가 된다. MATCH 함수의 세번째 인수가 1 이므로, MATCH 는 찾고자 하는 값인 2 이하의 값 중 가장 큰값을 찾게 된다. 에러인 #DIV/0! 은 무시하고, 가장 큰 값은 1 밖에 없으므로 가장 마지막 1 을 선택하게 되는 것이다.

만일 조건들에 부합하는 값이 없다면 #N/A 에러가 발생한다.
