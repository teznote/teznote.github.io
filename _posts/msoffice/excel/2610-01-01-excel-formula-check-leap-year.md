---
layout: default
title: "MOD: 어떤 연도가 윤년인지 아닌지 판별하는 함수식"
updated: 2022-03-27
tags: [msoffice,formula]
---

## 윤년 여부 판별식
```excel
= IF( MOD(연도,400)=0, "윤년", IF( MOD(연도,100)=0, "평년", IF( MOD(연도,4)=0, "윤년", "평년" )))
```
{:.excel}

윤년이 아닌 해는 "평년"으로 표현하였다. 이는 현대에서 일반적으로 사용하는 [그레고리력](https://namu.wiki/w/%EA%B7%B8%EB%A0%88%EA%B3%A0%EB%A6%AC%EB%A0%A5)의 윤년 규정을 그대로 함수식으로 옮겨놓은 것이다.

위 함수식에서 `연도` 자리에는 "2022" 처럼 정확히 숫자로 표현된 연도를 넣으면 된다.

## 다른 판별식 (비추천)

```excel
= IF( MONTH( DATE( 연도, 2, 29 )) = 2, "윤년", "평년" )
```
{:.excel}

MONTH, DATE 함수로도 윤년인지 아닌지를 판별할 수 있다. 어떤 `연도`의 2 월 29 일이 실제로 존재하는지를 판단한다. 참고로 윤년이 아니라면 `MONTH( DATE( 연도, 2, 29 ))` 의 결과는 3 이 되어 함수식 결과는 "평년"이 된다.

보다 간단해 보이지만 사용하지 않는 것이 좋다. **엑셀에는 1900 년을 윤년이 아님에도 윤년으로 판단하는 버그**가 있기 때문이다.

실제로 함수식 연도 자리에 "1900" 을 대입하면 "윤년" 이라는 결과가 나온다. 또한 엑셀 셀에 "1900-2-29" 라고 입력하면 날짜 형식으로 제대로 인식도 한다. 이는 버그이기는 하지만, 엑셀이 일부러 고치지 않고 있다. [MS 도움말](https://docs.microsoft.com/ko-KR/office/troubleshoot/excel/wrongly-assumes-1900-is-leap-year)을 참고하자면, 엑셀 이전의 소프트웨어들과 호환성을 유지하기 위해 당시 소프트웨어가 안고 있던 버그도 그대로 수용했다고 한다.

또다른 문제점도 있다. 엑셀은 날짜를 1900 년 1 월 1 일 부터 9999 년 12 월 31 일 까지만 인식한다. DATE, MONTH 함수도 엑셀 날짜를 이용하기 때문에 1900 ~ 9999 년 이외의 연도는 제대로 된 결과가 나오지 않는다. (물론 윤년버그로 인해 1900 년은 무조건 잘못된 결과가 나온다.)