---
layout: post
title: "INDEX, MATCH: VLOOKUP 보다 유용하게 사용할 수 있는 함수식"
updated: 2023-05-08
tags: [excel_formula]
---

VLOOKUP 은 엑셀에선 자주 사용되는 요긴한 함수지만 아래와 같은 불편한 점들이 있다.

- 검색기준열은 검색범위의 가장 왼쪽열로 항상 고정
- 두가지 이상 검색조건으로 검색 어려움
- 검색은 항상 세로열 방향으로만 가능하여 면으로 검색 (마치 VLOOKUP 과 HLOOKUP 을 혼합해놓은 검색) 불가능
- 넓은 검색범위로 인해 검색범위 안에 열삽입/삭제 하게되면 VLOOKUP 의 결과가 틀어질 가능성 높음
- 영문 대/소문자 구별 검색 불가능
{:.note}

하지만 VLOOKUP 대신 INDEX, MATCH 를 사용하면 VLOOKUP 과 동일한 결과를 보이면서도, 위 불편함을 최소화하면서 보다 유연하게 사용할 수 있다.

## INDEX, MATCH 기본 사용

![그림00](/img/excel_formula/formula-0010-00.png)

```excel
= VLOOKUP( 검색값, 검색범위, 열번호, false)
= INDEX( 검색대상열, MATCH( 검색값, 검색기준열, 0 ))
```
{:.excel}

위는 VLOOKUP 과 INDEX, MATCH 의 비교다.

VLOOKUP 은 `검색범위`를 넓게 지정하여, 가장 왼쪽열을 `검색기준열`로 삼고, `열번호`만큼 오른쪽으로 떨어진 열을 `검색대상열`로 삼지만, INDEX, MATCH 는 `검색기준열`과 `검색대상열`을 독립적으로 각각 지정한다. 이 때문에 오른쪽으로부터의 검색도 쉽고, `검색범위`의 열삽입/삭제에 영향을 받지도 않는다.

## 여러 사용법 예시

### 1. 오른쪽열 기준으로 보다 왼쪽열 검색

![그림01](/img/excel_formula/formula-0010-01.png)

사실 오른쪽 기준으로 보다 왼쪽을 검색하는 것은 다른 함수의 도움을 받아 VLOOKUP 로도 구현할 수는 있다. [별도 포스팅](/post/excel-formula-vlookup-from-right-to-left)을 참고하자.

### 2. 두가지 이상 조건으로 검색

![그림02](/img/excel_formula/formula-0010-02.png)

```excel
{= INDEX( 검색대상열, MATCH( 1, (검색기준열1=검색값1)*(검색기준열2=검색값2), 0 ))}
```
{:.excel}

수식 앞뒤로 붙어있는 중괄호 {} 를 제외한 수식을 타이핑하고 Ctrl + Shift + Enter 키를 눌러 **배열수식으로 입력**을 해야 정상작동 한다. 수식 입력셀에 포커싱을 두면, 입력한 수식 앞뒤로 중괄호가 보이는데, 배열수식으로 입력했음을 나타내는 표시다.

MATCH 함수 두번째 인수를 보면 `(조건식)*(조건식)` 형태로 되어 있는데, 각 조건식의 결과는 true 혹은 false 다. 엑셀은 이 값에 사칙연산을 가하면 true 는 1 로, false 는 0 으로 치환하여 계산을 한다. 모든 조건식이 true 가 되어야만 곱셈의 결과가 1 이 되므로, 모든 조건을 만족하는 경우를 찾도록 구성되어 있는 셈이다.

사실 VLOOKUP 으로도 두가지 이상 조검 검색이 가능하다. [별도 포스팅](/post/excel-formula-multiple-criteria-vlookup)을 참고하자.

### 3. 면으로 검색 (VLOOKUP 과 HLOOKUP 의 혼합)

![그림03](/img/excel_formula/formula-0010-03.png)

```excel
= INDEX( 검색대상범위, MATCH( 검색값, 검색기준열, 0 ), MATCH( 검색값, 검색기준행, 0 ))
```
{:.excel}

INDEX 안에 MATCH 가 두번 나오는데, 첫번째 MATCH 는 열방향(세로방향)으로 검색을, 두번째는 행방향(가로방향)으로 검색을 한다고 이해하면 된다.

### 4. 대/소문자 구별 검색

![그림04](/img/excel_formula/formula-0010-04.png)

```excel
{= INDEX( 검색대상열, MATCH( true, EXACT( 검색값, 검색기준열 ), 0 ))}
```
{:.excel}

수식 앞뒤로 붙어있는 중괄호 {} 를 제외한 수식을 타이핑하고 Ctrl + Shift + Enter 키를 눌러 **배열수식으로 입력**을 해야 정상작동 한다. 수식 입력셀에 포커싱을 두면, 입력한 수식 앞뒤로 중괄호가 보이는데, 배열수식으로 입력했음을 나타내는 표시다.

대/소문자 구별의 핵심은 EXACT 함수인데, 이 함수를 입력할 때 보면 도움말 팝업에 "대/소문자를 구분합니다." 라고 나온다. EXACT 의 한쪽 인수를 셀 하나로 두고, 다른쪽 인수를 범위로 두어 배열수식으로 입력하면, 일일이 대소문자 하나하나를 비교하여 완전일치 여부를 다시 배열로 반환한다. 이를 INDEX, MATCH 가 받아서 처리하는 구조이다.